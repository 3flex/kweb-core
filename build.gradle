allprojects {
    buildscript {
        repositories {
            jcenter()
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
    }
}

buildscript {
    ext.kotlin_version = '1.2.10'
    ext.dokka_version = '0.9.15'
    ext.ktor_version = '0.9.0'

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10'
    }
}

group 'io.kweb'
version '0.2.12'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'
apply plugin: "info.solidsoft.pitest"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/ktor"
    }
  /*  maven {
        url "http://dl.bintray.com/cy6ergn0m/maven"
    } */
    maven {
        url 'https://jitpack.io'
    }
    jcenter()
}

configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}


dependencies {
    compile 'com.github.salomonbrys.kotson:kotson:2.3.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.20'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:0.20'

    compile ("io.ktor:ktor-server-core:$ktor_version") {
        exclude group : 'ch.qos.logback', module : 'logback-classic'
    }

    compile ("io.ktor:ktor-server-netty:$ktor_version") {
        exclude group : 'ch.qos.logback', module : 'logback-classic'
    }
    compile("io.ktor:ktor-websockets:$ktor_version") {
        exclude group : 'ch.qos.logback', module : 'logback-classic'
    }

    compile('io.mola.galimatias:galimatias:0.2.0')

    compile 'io.github.microutils:kotlin-logging:1.4.9'
    compile("org.reflections:reflections:0.9.11")

    compile 'com.github.kwebio:shoebox:0.2.13'

    testCompile 'net.sourceforge.htmlunit:htmlunit:2.26'
    testCompile 'io.kotlintest:kotlintest:2.0.3'
    testCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'

    // TODO: This should be testCompile
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'

    // Should be compileOnly
    compileOnly 'com.github.HotswapProjects.HotswapAgent:hotswap-agent-core:1.1.0-SNAPSHOT'

}

// Needed because there's something messy with kotlin version numbers and dependencies
/*
configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion "$kotlin_version"
            }
        }
    }
}
*/


kotlin { experimental { coroutines 'enable' } }

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'html'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}

