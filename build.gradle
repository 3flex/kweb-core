allprojects {
    buildscript {
        repositories {
            jcenter()
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
    }
}

buildscript {
    ext.kotlin_version = '1.2.50'
    ext.dokka_version = '0.9.15'
    ext.ktor_version = '0.9.3'

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.10'
    }
}

group 'io.kweb'
version '0.3.2'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'
apply plugin: "info.solidsoft.pitest"
apply plugin: 'org.junit.platform.gradle.plugin'

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/ktor"
    }
    maven {
        url 'https://jitpack.io'
    }
    maven { url "http://dl.bintray.com/jetbrains/spek" }
    jcenter()
}

configurations {
    provided
}

configurations.all {
    resolutionStrategy {
        force 'org.jetbrains.kotlinx:kotlinx-coroutines-io:0.21-alpha-2'
    }
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}


dependencies {
    compile 'com.github.salomonbrys.kotson:kotson:2.3.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    //////////////////////////////
    // Kotlin library dependencies
    //////////////////////////////
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.21-alpha-2'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:0.21-alpha-2'

    // TODO: This should be testCompile
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'
    // Should be compileOnly
    compile group: 'org.hotswapagent', name: 'hotswap-agent-core', version: '1.1.0'

    ////////////////////
    // Ktor dependencies
    ////////////////////
    compile ("io.ktor:ktor-server-core:$ktor_version") {
        exclude group : 'ch.qos.logback', module : 'logback-classic'
    }

    compile ("io.ktor:ktor-server-netty:$ktor_version") {
        exclude group : 'ch.qos.logback', module : 'logback-classic'
    }
    compile("io.ktor:ktor-websockets:$ktor_version") {
        exclude group : 'ch.qos.logback', module : 'logback-classic'
    }

    compile('io.mola.galimatias:galimatias:0.2.0')

    compile 'io.github.microutils:kotlin-logging:1.4.9'
    compile("org.reflections:reflections:0.9.11")

    compile 'com.github.kwebio:shoebox:0.2.17'

    compile 'com.github.yamamotoj:cached-property-kotlin:0.1.0'

    ///////////////////////////
    // Dependencies for testing
    ///////////////////////////
    compile group: 'net.sourceforge.htmlunit', name: 'htmlunit', version: '2.29'
    testCompile ('org.jetbrains.spek:spek-api:1.1.5') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntime ('org.jetbrains.spek:spek-junit-platform-engine:1.1.5') {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation 'org.amshove.kluent:kluent:1.35'
    testCompile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'


}

// Needed because there's something messy with kotlin version numbers and dependencies
/*
configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion "$kotlin_version"
            }
        }
    }
}
*/


kotlin { experimental { coroutines 'enable' } }

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'html'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
}

// TODO: Figure out why this is needed and remove: https://discourse.corda.net/t/m12-1-intellij-complains-cannot-inline-bytecode-build-with-jvm-target-1-8-into-bytecode-that-is-being-built-with-jvm-target-1-6/1249/3
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}